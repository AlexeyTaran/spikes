buildscript {
  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.2'
  }
}

apply plugin: 'com.android.application'
apply plugin: com.novoda.buildproperties.BuildPropertiesPlugin

buildProperties {
  secrets {
    file rootProject.file('properties/secrets.properties')
  }
  resValues {
    file rootProject.file('properties/resValues.properties')
  }
  debugSigning {
    file rootProject.file('properties/debugSigning.properties')
  }
  releaseSigning {
    file rootProject.file('properties/releaseSigning.properties')
  }
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "com.novoda.buildpropertiesplugin.sample"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    buildConfigBoolean 'TEST_BOOLEAN', false
    buildConfigInt 'TEST_INT', 42
    buildConfigLong 'TEST_LONG', Long.MAX_VALUE
    buildConfigDouble 'TEST_DOUBLE', Math.PI
    buildConfigString 'TEST_STRING', "whateva"
    buildConfigProperty 'OVERRIDABLE', buildProperties.secrets['overridable']
    buildConfigProperty 'GOOGLE_MAPS_KEY', buildProperties.secrets['googleMapsKey']
    buildConfigProperty 'SUPER_SECRET', buildProperties.secrets['superSecret']
    buildConfigProperty 'FOO', buildProperties.secrets['notThere'].or('bar')
    buildConfigProperty 'WAT', buildProperties.env['WAT']
  }

  signingConfigs {
    debug {
      signingConfigProperties buildProperties.debugSigning
    }
    release {
      signingConfigProperties buildProperties.releaseSigning
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
      buildConfigProperty 'ONLY_DEBUG', buildProperties.secrets['superSecret']
      resValueInt 'debug_test_int', 100
      resValueBoolean 'debug_test_bool', true
      resValueString 'debug_test_string', 'dunno bro...'
      resValueProperty 'super_secret', buildProperties.secrets['superSecret']
      resValueProperty 'google_maps_key', buildProperties.resValues['google_maps_key']
      resValueProperty 'another_key', buildProperties.resValues['another_key']
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt')
      signingConfig signingConfigs.release
    }
  }
}

dependencies {
  compile 'com.android.support:appcompat-v7:24.0.0'
}
